#!/usr/bin/perl -w

use strict;
use warnings;
use 5.006;
use Time::HiRes qw( gettimeofday tv_interval sleep );

# Make sure we have the proper dependencies installed
my @deps = qw(
  Net::SAPO::Broker
  Getopt::Long
  LWP::UserAgent
);

foreach my $module (@deps) {
	eval "require $module; import $module ();";
	if ($@) {
	  my $mesg = <<"    OOPS";
	    ERROR: You seem to be missing the '$module' module.
	    
	    Unfortunately I require that module to do my job. Use:

	      cpan $module
	      
	    to install it.
    OOPS
    _write_err_mesg($mesg);
    exit(1);
	}
}

# Parse command line

my (
  $subscribe, $publish,
  $queue, $remove,
  $count, $rate, $size,
  $collector,
  $quiet, $help,
  $host, $port,
);
$quiet = 1 unless -t \*STDIN;
$size  = 70;
$count = 1000;

my $ok = GetOptions(
  "subscribe" => \$subscribe,
  "publish"   => \$publish,
  
  "queue=s"   => \$queue,
  "remove"    => \$remove,
  
  "count=s"   => \$count,
  "rate=s"    => \$rate,
  "size=s"    => \$size,
  
  "collector" => \$collector,
  
  "host=s"    => \$host,
  "port=s"    => \$port,
  
  "quiet"     => \$quiet,
  "help"      => \$help,
);

usage() unless $ok;
usage() if $help;

usage("Invalid --rate, must be 0 or greater")
  if defined($rate) && $rate < 0;

usage("Minimal payload size is 10")
  if $size < 10;

usage("The remove operation requires a queue identifier")
  if $remove && !$queue;

# check for incompatible modes
my $mode;

if (defined($rate)) {
  $mode = "load generation";
}

if ($remove) {
  usage("Queue operations and $mode operations are incompatible")
    if $mode;
  $mode = 'queue operations';
}

if ($collector) {
  usage("Load collector operations and $mode operations are incompatible")
    if $mode;
  $mode = 'load collector';
}

if ($publish || $mode) {
  usage("Only one off 'subscribe' or 'publish' can be used")
      if $subscribe && $publish;
  usage("Basic pubsub operations and $mode operations are incompatible")
    if $mode && ($subscribe || $publish);
  $mode = 'basic pubsub';
}

my @topics = @ARGV;
@ARGV = ();

# Run the proper role
if ($publish) {
  usage("Need set of topics to publish") unless @topics;
  publisher();
}
elsif ($queue && $remove) {
  queue_remove();
}
elsif (defined($rate)) {
  usage("Need set of topics for load generation") unless @topics;
  load_generator();
}
elsif ($collector) {
  usage("Need set of topics for load collection") unless @topics;
  load_collector();
}
else {
  usage("Need set of topics to subscribe") unless @topics;
  subscriber();
}


### Roles

sub subscriber {
  my $sb = get_agent_connection();
  
  foreach my $topic (@topics) {
    $sb->subscribe({
      topic      => $topic,
      on_success => sub {
        diag("%% Subscribed topic '$topic'.");
      },
      on_error => sub {
        diag("ERROR subscribing to topic '$topic':");
        foreach my $e (@_) {
          next if ref($e);
          diag("    $e");
        }
      },
      on_message => sub {
        my (undef, $payload, $topic) = @_;
        my ($now, $count, $rate, $delta) = _calc_rate($topic);

        my $rt = sprintf('%0.3f mesg/sec (%d messages)', $rate, $count);
        my $ts = localtime($now->[0]);
        substr($ts, -5, 0, '.'.substr($now->[1], 0, 4));
        $ts .= sprintf(' (%0.3f elapsed since last message)', $delta) if $delta;
        
        my $len = length($payload);

        diag("** Got message on topic '$topic':");
        diag("** at $ts");
        diag("** rate for topic is $rt");
        diag();
        diag("-- Message starts (length $len)");
        print $payload,"\n";
        diag("-- Message ends");
        diag();
      }
    })
  }
  
  # Temp hack, deliver_messages() should wait forever
  while ($sb->state eq 'connected') { $sb->deliver_messages(5) };
}

sub publisher {
  my $sb = get_agent_connection();
  
  diag('Enter a message per line. Each non-empty line will be sent');
  diag('to the following topics:');
  foreach my $topic (@topics) {
    diag("    * $topic");
  }
  diag("");
  diag('Type ^D to exit');
  diag();
  
  while (my $msg = <>) {
    chomp($msg);
    next if $msg =~ m/^\s*$/;
    
    broadcast($sb, $msg);
  }
}

sub queue_remove {
  my $ua = LWP::UserAgent->new;
  $ua->timeout(30);
  
  # Hack! Move this to Protocol eventually
  $host ||= '127.0.0.1';
  my $url = "http://$host:3380/broker/admin";
  my $response = $ua->post($url, Content => "QUEUE:$queue");
  
  if (! $response->is_success) {
    diag("ERROR while removing queue '$queue'");
    diag("Full HTTP response: \n",$response->as_string);
    
    exit(1);
  }
  
  return;
}

sub load_generator {
  my $sb = get_agent_connection();
  
  my $delay = 0;
  $delay = 1 / $rate if $rate > 0;

  if ($count > 0) {
    diag("** Generating $count messages (payload size is $size)");
  }
  else {
    $count = 0;
    diag("** Generating unlimited messages (payload size is $size)");
  }
  diag("** at a rate of $rate mesg/sec");
  diag("** You can abort with ^C at any time");

  diag("** Running....");
  broadcast($sb, "--- start: count $count rate $rate size $size");
  my $start = [gettimeofday()];  
  my $mesg_number = 0;
  while (1) {
    $mesg_number++;
    my $payload = "N=$mesg_number;";
    $payload .= ' ' x ($size - length($payload));
    
    broadcast($sb, $payload);
    sleep($delay) if $delay;
    
    next unless $count;
    $count--;
    last unless $count;
  }
  
  my $actual_rate = $mesg_number / (tv_interval($start) || 1);
  broadcast($sb, "--- end: actual rate $actual_rate");
  diag('** Done!');
  diag("** Sent $mesg_number messages, requested rate $rate mesg/sec");
  diag("** Actual rate was $actual_rate mesg/sec");
}

sub load_collector {
  my %load_stats;
  
  my $sb = get_agent_connection();
  
  foreach my $topic (@topics) {
    $sb->subscribe({
      topic      => $topic,
      on_success => sub {
        diag("%% Collecting on topic '$topic'.");
      },
      on_error => sub {
        diag("ERROR subscribing to topic '$topic':");
        foreach my $e (@_) {
          next if ref($e);
          diag("    $e");
        }
      },
      on_message => sub {
        my (undef, $payload, $topic) = @_;
        
        if ($payload =~ m/--- start: count (\d+) rate ([\d.]+) size (\d+)/) {
          my ($count, $rate, $size) = ($1, $2, $3);
          $load_stats{$topic} = {
            count   => $count,
            rate    => $rate,
            size    => $size,
            start_t => [gettimeofday()],
            last_m  => 0,
            recv_n  => 0,
            ooo_n   => 0,
            miss_n  => 0,
            mesg_t  => [],
          };
          diag("-- Starting load collector run for topic '$topic'");
          diag("-- Parameters: count $count, rate $rate, size $size");
          return;
        }
        
        if ($payload =~ m/--- end: actual rate ([\d.]+)/) {
          my $rate = $1;
          my $t = delete $load_stats{$topic};
          return unless $t;
          
          $t->{end_t}   = [gettimeofday()];
          $t->{lg_rate} = $rate;
          $t->{delta}   = tv_interval($t->{start_t}, $t->{end_t});
          $t->{lc_rate} = $t->{recv_n} / ($t->{delta} || 1);
          my ($miss_n, $mesg_t) = (0, $t->{mesg_t});
          for (my $i = 1; $i <= $t->{count}; $i++) {
            $miss_n++ unless defined $mesg_t->[$i];
          }
          $t->{miss_n} = $miss_n;

          diag("-- Load collector run for topic '$topic' done!");
          diag("** Parameters: count $t->{count}, rate $t->{rate}, size $t->{size}");
          diag("** Elapsed seconds: $t->{delta}");
          diag("** Received messages: $t->{recv_n}");
          diag("** Missed messages: $t->{miss_n}");
          diag("** Out-of-order messages: $t->{ooo_n}");
          diag("** Load Collector actual rate: $t->{lc_rate}");
          diag("** Load Generator actual rate: $t->{lg_rate}");
          return;
        }
        
        if (my ($cur) = $payload =~ m/^N=(\d+);/) {
          my $t = $load_stats{$topic};
          return unless $t;
          
          $t->{recv_n}++;
          $t->{mesg_t}[$cur] = [gettimeofday()];
          my $diff = $cur - $t->{last_m} - 1;
          if ($diff >= 0) {
            $t->{last_m} = $cur;
          }
          else {
            $t->{ooo_n}++;
          }
        }
      }
    });
  }
  
  diag("Waiting for load generator to start");
  
  # Temp hack, deliver_messages() should wait forever
  while ($sb->state eq 'connected') { $sb->deliver_messages(1) };
}


### Agent connection

sub get_agent_connection {
  my $sb = Net::SAPO::Broker->new({
    host => $host,
    port => $port,

    on_state_connecting => sub {
      my ($sb) = @_;
      my $agent = $sb->host;
      diag("## Connecting to agent at '$agent'");
    },
    on_connected => sub {
      diag("## Connected!");
    },
    on_eof => sub {
      diag("## Lost connection to agent");
    },
    on_state_disconnecting => sub {
      diag("## Disconnecting");
    },
    on_reconnect => sub {
      diag("## Reconnecting");
    },
    on_read_error => sub {
      my (undef, $e) = @_;
      diag("## Read error: $e");
    },
    on_write_error => sub {
      my (undef, $e) = @_;
      diag("## Write error: $e");
    },
  });
  
  if ($sb->state ne 'connected') {
    my ($h, $p) = ($sb->host, $sb->port);
    diag("FATAL: could not connect to agent at '$h' port $p");
    exit(1);
  }
  
  return $sb;
}

### Multi-topic publish

sub broadcast {
  my ($sb, $payload) = @_;
  
  foreach my $topic (@topics) {
    $sb->publish({
      topic   => $topic,
      payload => $payload,

      # on_error => sub {
      #   diag("!! Could not send message to topic '$topic'");
      #   diag("!! Payload is: $payload");
      # },
    });
  }
  
  return;
}


### Diagnostics

sub diag {
  print @_,"\n" unless $quiet;
}


### Rate calculations

our %topic_rate;

sub _calc_rate {
  my ($topic) = @_;
  my $now = [gettimeofday()];
  my ($delta, $rate, $count);
  
  my $stats = $topic_rate{$topic};
  
  if ($stats) {
    $count      = ++$stats->[1];
    $delta      = tv_interval($stats->[2], $now);
    $stats->[2] = $now;
    
    my $since_start = tv_interval($stats->[0], $now);
    $rate = $count / ($since_start||1);
  }
  else {
    $topic_rate{$topic} = [$now, 1, $now];
    $delta = 0;
    $rate  = 0;
    $count = 1
  }

  return ($now, $count, $rate, $delta);  
}


### Usage

sub usage {
  my ($mesg) = @_;
  
  print STDERR "ERROR: $mesg\n" if $mesg;
  _write_err_mesg(<<"  USAGE");
    Usage: sapo-broker-client [options] [topic...]
    
     Basic Publish/Subscribe:
        --subscribe   prints new messages from any of the topics
        --publish     sends each non-empty lines from stdin to all the topics
      
        * --subscribe is the default;
        * You cannot use --subscribe and --publish simultaneously.
        
      
     Queue manipulations:
        --queue       specify the queue to work with
        --remove      removes the specified queue
        
    
     Load Generation:
        --count=N     how many messages to generate [defaults to 1000]
        --size=N      size of each payload [default to 70 bytes]
        --rate=N      desired messages per second (approximation)
        
        * rate can be decimal value, must be 0 or above;
        * rate of 0 means maximum speed (useful to test slow clients);
        * rate is an approximation, does not take in account over-head;
        * minimal size is 10.
        
        Rates above 4000 are very unreliable due to usleep granularity.
        
    
     Load Collector:
        --collector   starts a load collector for each topic
        
        It will wait for a start of a run using the load generation mode
        on the same topic. After the end of the run, prints actual rate,
        missed messages and out-of-order messages.
        
        
     General options:
        --host        connect to a specific host [defaults to 127.0.0.1]
        --port        connect to a specific TCP port [default to 3322]
        --quiet       disables diagnostic messages
        --help        shows this file
        
        * If either STDIN or STDOUT is not a tty, --quiet is turned on.
        
  USAGE
  exit(1);
}

sub _write_err_mesg {
  my ($mesg) = @_;
  
  my ($prefix) = $mesg =~ /^(\s+)/;
  $mesg =~ s/^$prefix//gsm;
  print STDERR "\n",$mesg,"\n";
}